name := "tailscale"
packages := "tailscale"
files := "usr"
# TODO: Disabled as this interactively query for confirmation and can not be bypassed
# external_repos := "https://pkgs.tailscale.com/stable/fedora/tailscale.repo"
# TODO: Workaround below but something weird is going on here
pre_commands := "dnf config-manager addrepo --from-repofile=https://pkgs.tailscale.com/stable/fedora/tailscale.repo && sed -i 's/repo_gpgcheck=1/repo_gpgcheck=0/g' /etc/yum.repos.d/tailscale.repo"
upholds := "
tailscaled.service
"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:43 x86_64,aarch64
"

import '../sysext.just'

all: default

install-manual:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    # Move service from /lib to /usr/lib as needed
    if [[ -f "lib/systemd/system/tailscaled.service" ]]; then
        ${SUDO} mv lib/systemd/system/tailscaled.service usr/lib/systemd/system/
        ${SUDO} rmdir lib/systemd/system lib/systemd lib
    fi

    # Fix systemd unit using a default config in /etc as needed
    if [[ -f "etc/default/tailscaled" ]]; then
        ${SUDO} install -d -m 0755 -o 0 -g 0 usr/lib/systemd/system/tailscaled.service.d
        ${SUDO} tee usr/lib/systemd/system/tailscaled.service.d/sysext.conf << 'EOF'
    [Service]
    EnvironmentFile=
    EnvironmentFile=/usr/etc/default/tailscaled
    EnvironmentFile=-/etc/default/tailscaled
    EOF
    fi
