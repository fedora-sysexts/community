name := "vscodium"
packages := "codium"

import '../sysext.just'

all: default

# Custom download step to get VSCodium RPM
download-rpms target arch=arch:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    mkdir rpms
    cd rpms

    arch="{{arch}}"

    {{retry_function}}

    echo "⬇️ Downloading VSCodium"
    url="https://api.github.com/repos/VSCodium/vscodium/releases/latest"
    retry 5 60 curl --fail --silent "${url}" --output json
    release="$(cat json | jq -r '.tag_name')"
    rpmurl="$(cat json | jq -r '.assets[] | select(.name == "codium-'${release}'-el8.'${arch}'.rpm") | .browser_download_url')"
    wget "${rpmurl}"{,.sha256}
    sha256sum -c ./*.rpm.sha256
    rm ./*.rpm.sha256 ./json
